;;;; -*- Mode: Lisp -*-
;;;; $Id$

(in-package :snmp)

(defparameter *preload-mibs*
  '("MIB:IETF;SNMPv2-SMI"
    "MIB:IETF;SNMPv2-TC"
    "MIB:IETF;SNMPv2-TM"
    "MIB:IETF;SNMPv2-CONF"
    "MIB:IETF;SNMPv2-MIB"
    "MIB:IANA;IANAifType-MIB"

    "MIB:LISP;LISP-MIB.TXT"
    "MIB:LISP;ABCL-MIB.TXT"
    "MIB:LISP;CLOZURE-MIB.TXT"
    "MIB:LISP;CMUCL-MIB.TXT"
    "MIB:LISP;ECL-MIB.TXT"
    "MIB:LISP;FRANZ-MIB.TXT"
    "MIB:LISP;LISPWORKS-MIB.TXT"
    "MIB:LISP;SBCL-MIB.TXT"
    "MIB:LISP;SCL-MIB.TXT"))

(defun compile-mib (&rest args)
  (apply #'compile-asn.1 args))

(defun load-mib (&rest args)
  (apply #'load-asn.1 args))

(defvar *mib-expression* "SNMP:MIB.LISP-EXPR")
(defvar *mib-dependency-file* "SNMP:MIB-DEPEND.LISP")

(defvar *pathname-base*)

(eval-when (:compile-toplevel :load-toplevel :execute)
  (defmacro make-mib-pathname (type pathname)
    `(merge-pathnames (make-pathname :name (pathname-name (translate-logical-pathname ,pathname))
                                     :type ,type
                                     :directory (append (pathname-directory *pathname-base*)
                                                        '("compiled-mibs")))
                      *pathname-base*)))

(defun lisp-file (pathname)
  (make-mib-pathname "lisp" pathname))

(defun expr-file (pathname)
  (make-mib-pathname "lisp-expr" pathname))

(defun update-mib (&optional (mib-list *preload-mibs*))
  "Update mib.lisp-expr"
  (setq *pathname-base*
        (translate-logical-pathname "SNMP:"))
  (let ((mib.lisp-expr '())
        (mib-depend.lisp '())
        (*package* *asn.1-package*))
    (dolist (i mib-list)
      (format t "; Compiling ~A~%" i)
      (compile-asn.1 i :to (lisp-file i))
      (let ((depends (with-open-file (s (expr-file i) :direction :input)
                       (read s)))
            (name (string-downcase (pathname-name (lisp-file i)))))
        (push (if (cdr depends)
                  `(:file ,name
                    :depends-on ,(mapcar #'(lambda (x) (string-downcase (symbol-name x)))
                                         (cdr depends)))
                `(:file ,name))
              mib.lisp-expr)
        (if (cdr depends)
            (push depends mib-depend.lisp))))
    ;;; Update MIB Dependency, it's for ASDF
    (with-open-file (s *mib-expression*
                       :direction :output
                       :if-exists :supersede)
      (format s ";;;; -*- Mode: Lisp -*-~%;;;; Generated by #p\"SNMP:UPDATE-MIB.LISP\"~%")
      (pprint (setf mib.lisp-expr (nreverse mib.lisp-expr)) s)
      (terpri s))
    ;;; Update MIB Dependency, it's for MIB Browser
    (with-open-file (s *mib-dependency-file*
                       :direction :output
                       :if-exists :supersede)
      (format s ";;;; -*- Mode: Lisp -*-~%;;;; Generated by #p\"SNMP:UPDATE-MIB.LISP\"~%")
      (dolist (i `((in-package :asn.1)
                   :break-line
                   (eval-when (:load-toplevel :execute)
                     (mapcar #'(lambda (asn.1::x)
                                 (setf (gethash (car asn.1::x)
                                                asn.1:*mib-module-dependency*)
                                       (cdr asn.1::x)))
                             ',mib-depend.lisp))))
        (if (eq i :break-line)
            (terpri s)
          (pprint i s)))
      (terpri s))
    (asdf:load-system :snmp-mib)
    (pprint mib.lisp-expr)))
